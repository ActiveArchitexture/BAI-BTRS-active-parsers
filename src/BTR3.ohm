BTR3 {
	BTRSfile = FileHeader Bank* FileTrailer

    Bank = BankHeader Account* BankTrailer

    Account = AccountHeader TransactionDetail* AccountTrailer

    
    FileHeader = "01" delim senderIdentification delim receiverIdentification delim fileCreationDate delim fileCreationTime delim fileIdentificationNumber delim physicalRecordLength delim blockSize delim versionNumber eor
    senderIdentification = alnum +
    receiverIdentification = alnum +
    fileCreationDate = date
    fileCreationTime = time
    fileIdentificationNumber = alnum +
    physicalRecordLength = digit *
    blockSize = digit *
    versionNumber = bai1 | bai2 | bai3

    FileTrailer = "99" delim fileControlTotal delim numberofBanks delim numberofRecords eor
    fileControlTotal = optSignedN
    numberofBanks = optPosN
    numberofRecords = optPosN

    optSignedN = ("-" | "+")? digit+
    optPosN = "+"? digit+


    BankHeader = "02" delim ultimateReceiverIdentification delim bankIdentification delim groupStatus delim asofDate delim asofTime delim currencyCodeBank delim asofDateModifier eor
    ultimateReceiverIdentification = alnum *
    bankIdentification = alnum +
    groupStatus = "1"
    asofDate = date
    asofTime = time?
    currencyCodeBank    (Currency Code is null in BTR3 but not in BAI2) 
                        = letter*
    asofDateModifier = finalPreviousDay | interimSameDay | finalSameDay
    
    finalPreviousDay = "2"
    interimSameDay = "3"
    finalSameDay = "4"

    BankTrailer (Number of Accounts is in BTR3 but not in BAI2)
                = "98" delim groupControlTotal delim (numberofAccounts delim)? numberofRecords eor
    groupControlTotal = optSignedN
    numberofAccounts = optPosN

    AccountHeader = "03" delim customerAccountNumber delim currencyCodeAccount delim statusOrSummaryCodeFormatOptRepeat eor
    statusOrSummaryCodeFormatOptRepeat = statusOrSummaryCodeFormat (delim statusOrSummaryCodeFormat)*
    statusOrSummaryCodeFormat = statusCodeFormat | summaryCodeFormat
    customerAccountNumber = alnum *
    currencyCodeAccount (Currency Code is mandatory in BTR3 but not in BAI2) 
                        = letter*

    statusCodeFormat = statusTypeCode delim amount delim itemCountNull delim fundsTypeNull
    statusTypeCode  = ("0" digit digit) -- zero99
                    | ("7" "0" digit) -- seven09
                    | ("9" "0" digit) -- nine09
                    | ("9" "1" digit) -- nine19

    amount  (Amount is mandatory in BTR3 but not in BAI2) 
            = optSign digit*
    itemCountNull = ""
    fundsTypeNull = ""


    summaryCodeFormat = summaryTypeCode delim amountOpt delim itemCount delim fundsType
    summaryTypeCode = 
                    | ("1" .. "7" digit digit) -- onetoseven99
                    | ("9" "2" digit) -- nine29
                    | ("9" "3" "1".."4") -- nine34
                    | ("9" "6" digit) -- nine69
                    | ("9" "7" "1".."4") -- nine74
                    | "" -- nullSummaryTypeCode

    amountOpt = optSign digit*
    itemCount = digit*
    fundsType = alnum?

    AccountTrailer = "49" delim accountControlTotal delim numberofRecords eor
    accountControlTotal = optSignedN

    TransactionDetail = "16" delim detailTypeCode delim detailAmount delim detailFundsType delim bankReferenceNumber delim customerReferenceNumber delim detailText eor?
    detailTypeCode = "1" .. "9" digit digit
    detailAmount = optPos digit*
    detailFundsType (Funds Type is 0, 1, 2, Z OR S OR V in BTR3 but optional in BAI2) 
                    = detailFundsTypeS | detailFundsTypeV | detailFundsTypeZ012

    detailFundsTypeZ012 (Funds Type is 0, 1, 2, Z in BTR3 but optional in BAI2) 
                        = "Z" | "0" | "1" | "2" | ""

    detailFundsTypeS = "S" delim availableImmediate delim available1Day delim available2PlusDays
    availableImmediate = detailAvailableAmount
    available1Day = detailAvailableAmount
    available2PlusDays = detailAvailableAmount

    detailFundsTypeV = "V" delim valueDate delim valueTime
    valueDate = date
    valueTime = time

    detailAvailableAmount = optSign digit*

    bankReferenceNumber = alphaNumeric*
    customerReferenceNumber = alphaNumeric*
    detailText  (Text relaxed for CBA BAI2 format does not contain , /  Optionally followed by /) 
                = (~ eor any) *

    optPos = "+"?
    optSign = ("-" | "+")?
    
    bai1 = "1"
    bai2 = "2"
    bai3 = "3"

    date = yy mo dd
    time = hh mm
    
    yy = digit digit
    mo = ("0" digit) | ("1" "0" .. "2")
    dd = "0".."3" digit
    hh = ("0".."1" digit) | ("2" "0" .. "3")
    mm = "0".."5" digit

    delim = ","
    eor = "/"

    alphaNumeric    (BTR3 Alphanumeric - string not containing comma or slash) 
                    = ~(delim | eor) any

}