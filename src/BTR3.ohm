BTR3 {
	BTRSfile = FileHeader Bank* FileTrailer

    Bank = BankHeader Account* BankTrailer

    Account = AccountHeader AccountTrailer

    
    FileHeader = "01" delim senderID delim receiverID delim fileCreationDate delim fileCreationTime delim fileID delim physicalRecordLength delim blockSize delim versionNumber eor
    senderID = alnum +
    receiverID = alnum +
    fileCreationDate = date
    fileCreationTime = time
    fileID = alnum +
    physicalRecordLength = digit *
    blockSize = digit *
    versionNumber = bai1 | bai2 | bai3

    FileTrailer = "99" delim fileControlTotal delim numberofBanks delim numberofRecords eor
    fileControlTotal = optSignedN
    numberofBanks = optPosN
    numberofRecords = optPosN

    optSignedN = ("-" | "+")? digit+
    optPosN = "+"? digit+


    BankHeader = "02" delim ultimateReceiverID delim bankID delim groupStatus delim asofDate delim asofTime delim currencyCodeNull delim asofDateModifier eor
    ultimateReceiverID = alnum *
    bankID = alnum +
    groupStatus = "1"
    asofDate = date
    asofTime = time?
    currencyCodeNull = ""
    asofDateModifier = finalPreviousDay | interimSameDay | finalSameDay
    
    finalPreviousDay = "2"
    interimSameDay = "3"
    finalSameDay = "4"

    BankTrailer = "98" delim groupControlTotal delim numberofAccounts delim numberofRecords eor
    groupControlTotal = optSignedN
    numberofAccounts = optPosN

    AccountHeader = "03" delim customerAccountNumber delim currencyCode delim statusOrSummaryCodeFormatOptRepeat eor
    statusOrSummaryCodeFormatOptRepeat = statusOrSummaryCodeFormat (delim statusOrSummaryCodeFormat)*
    statusOrSummaryCodeFormat = statusCodeFormat | summaryCodeFormat
    customerAccountNumber = alnum *
    currencyCode = letter +

    statusCodeFormat = statusTypeCode delim amount delim itemCountNull delim fundsTypeNull
    statusTypeCode = "010"|"015"|"020"|"025"|"030"|"035"|"040"|"045"|"050"|"055"|"057"|"060"|"070"|"072"|"073"|"074"|
"076"|"077"|"078"|"079"|"080"|"081"|"701"|"703"|"705"|"707"|"709"

    amount = optSign digit+
    itemCountNull = ""
    fundsTypeNull = ""


    summaryCodeFormat = summaryTypeCode delim amountOpt delim itemCount delim fundsType
    summaryTypeCode = ("100"|"110"|"138"|"140"|"159"|"167"|"170"|"190"|"199"|"207"|"210"|"215"|"230"|"250"|"251"|"256"|
"270"|"280"|"294"|"307"|"355"|"356"|"371"|"390"|"400"|"416"|"438"|"450"|"459"|"467"|"470"|"490"|
"499"|"507"|"510"|"515"|"530"|"550"|"551"|"556"|"570"|"580"|"583"|"584"|"585"|"587"|"594"|"602"|
"628"|"630"|"632"|"655"|"671"|"690"|"720"|"760"|"920"|"921")?

    amountOpt = optSign digit*
    itemCount = digit*
    fundsType = alnum?

    AccountTrailer = "49" delim accountControlTotal delim numberofRecords eor
    accountControlTotal = optSignedN

    TransactionDetail = "16" delim detailTypeCode delim detailAmount delim detailFundsType delim bankReferenceNumber delim customerReferenceNumber delim detailText eor
    detailTypeCode = "115" | "399" | "495"
    detailAmount = optPos digit*
    detailFundsType = detailFundsTypeZ012 | detailFundsTypeS | detailFundsTypeV

    detailFundsTypeZ012 = "Z" | "0" | "1" | "2"

    detailFundsTypeS = "S" delim availableImmediate delim available1Day delim available2PlusDays
    availableImmediate = detailAvailableAmount
    available1Day = detailAvailableAmount
    available2PlusDays = detailAvailableAmount

    detailFundsTypeV = "V" delim valueDate delim valueTime
    valueDate = date
    valueTime = time

    detailAvailableAmount = optSign digit*

    bankReferenceNumber = alnum*
    customerReferenceNumber = alnum*
    detailText = alnum*


    optPos = "+"?
    optSign = ("-" | "+")?
    
    bai1 = "1"
    bai2 = "2"
    bai3 = "3"

    date = yy mo dd
    time = hh mm
    
    yy = digit digit
    mo = ("0" digit) | ("1" "0" .. "2")
    dd = "0".."3" digit
    hh = ("0".."1" digit) | ("2" "0" .. "3")
    mm = "0".."5" digit
    
    delim = ","
    eor = "/"
    
}